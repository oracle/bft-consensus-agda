{- Byzantine Fault Tolerant Consensus Verification in Agda, version 0.9.

   Copyright (c) 2020, 2021, Oracle and/or its affiliates.
   Licensed under the Universal Permissive License v 1.0 as shown at https://opensource.oracle.com/licenses/upl
-}
open import Optics.All
open import LibraBFT.Prelude
open import LibraBFT.Lemmas
open import LibraBFT.Base.KVMap
open import LibraBFT.Base.PKCS
open import LibraBFT.Hash
open import LibraBFT.Impl.Base.Types
open import LibraBFT.Impl.Consensus.Types
open import LibraBFT.Impl.Util.Crypto
open import LibraBFT.Impl.Handle
open import LibraBFT.Impl.Handle.Properties
open import LibraBFT.Concrete.System.Parameters
open        EpochConfig
open import LibraBFT.Concrete.System
open import LibraBFT.Concrete.Properties.Common
open import LibraBFT.Yasm.Yasm â„“-RoundManager â„“-VSFP ConcSysParms PeerCanSignForPK (Î» {st} {part} {pk} â†’ PeerCanSignForPK-stable {st} {part} {pk})

-- This module contains placeholders for the future analog of the
-- corresponding VotesOnce property.  Defining the implementation
-- obligation and proving that it is an invariant of an implementation
-- is a substantial undertaking.  We are working first on proving the
-- simpler VotesOnce property to settle down the structural aspects
-- before tackling the harder semantic issues.
module LibraBFT.Concrete.Properties.PreferredRound (ğ“” : EpochConfig) where
 import      LibraBFT.Abstract.Records UID _â‰ŸUID_ NodeId  ğ“” (ConcreteVoteEvidence ğ“”) as Abs
 open import LibraBFT.Concrete.Obligations.PreferredRound ğ“” (ConcreteVoteEvidence ğ“”)
 open import LibraBFT.Concrete.Properties.VotesOnce ğ“” as VO
 open WithAbsVote ğ“”
 open PeerCanSignForPK
 open PeerCanSignForPKinEpoch

 -- As with VotesOnce, we will have two implementation obligations, one for when v is sent by the
 -- step and v' has been sent before, and one for when both are sent by the step.

 PR-ImplObligationâ‚ : Set (â„“+1 â„“-RoundManager)
 PR-ImplObligationâ‚ =
   âˆ€{pid pid' s' outs pk}{pre : SystemState}
   â†’ (r : ReachableSystemState pre)
   -- For any honest call to /handle/ or /init/,
   â†’ (sps : StepPeerState pid (msgPool pre) (initialised pre) (peerStates pre pid) (s' , outs))
   â†’ âˆ€{mbr v vabs m v' v'abs m'}
   â†’ Meta-Honest-PK pk
   -- For signed every vote v of every outputted message
   â†’ v'  âŠ‚Msg m'  â†’ send m' âˆˆ outs
   â†’ (sig' : WithVerSig pk v') â†’ Â¬ (âˆˆGenInfo (ver-signature sig'))
   -- If v is really new and valid
   â†’ PeerCanSignForPK (StepPeer-post {pre = pre} (step-honest sps)) v' pid pk
   -- And if there exists another v' that has been sent before
   â†’ v âŠ‚Msg m â†’ (pid' , m) âˆˆ (msgPool pre)
   â†’ (sig : WithVerSig pk v) â†’ Â¬ (âˆˆGenInfo (ver-signature sig))
   -- If v and v' share the same epoch
   â†’ v ^âˆ™  vEpoch â‰¡ v' ^âˆ™ vEpoch
   -- and v is for a smaller round
   â†’ v ^âˆ™ vRound < v' ^âˆ™ vRound
   -- and vabs* are the abstract Votes for v and v'
   â†’ Î±-ValidVote ğ“” v  mbr â‰¡ vabs
   â†’ Î±-ValidVote ğ“” v' mbr â‰¡ v'abs
   â†’ (c2 : Cand-3-chain-vote vabs)
   -- then the round of the block that v' votes for is at least the round of
   -- the grandparent of the block that v votes for (i.e., the preferred round rule)
   â†’ Î£ (VoteParentData v'abs)
           (Î» vp â†’ Cand-3-chain-head-round c2 â‰¤ Abs.round (vpParent vp))
     âŠ (VoteForRoundâˆˆ pk (v' ^âˆ™ vRound) (v' ^âˆ™ vEpoch) (v' ^âˆ™ vProposedId) (msgPool pre))


 -- Similarly in case the same step sends both v and v'
 PR-ImplObligationâ‚‚ : Set (â„“+1 â„“-RoundManager)
 PR-ImplObligationâ‚‚ =
   âˆ€{pid s' outs pk}{pre : SystemState}
   â†’ (r  : ReachableSystemState pre)
   -- For any honest call to /handle/ or /init/,
   â†’ (sps : StepPeerState pid (msgPool pre) (initialised pre) (peerStates pre pid) (s' , outs))
   â†’ âˆ€{mbr v vabs m v' v'abs m'}
   â†’ Meta-Honest-PK pk
   -- For every vote v represented in a message output by the call
   â†’ v  âŠ‚Msg m  â†’ send m âˆˆ outs
   â†’ (sig : WithVerSig pk v) â†’ Â¬ (âˆˆGenInfo (ver-signature sig))
   -- If v is really new and valid
   â†’ Â¬ (MsgWithSigâˆˆ pk (ver-signature sig) (msgPool pre)) -- âˆ„[ v'' ] VoteForRoundâˆˆ ... ?
   â†’ PeerCanSignForPK (StepPeer-post {pre = pre} (step-honest sps)) v pid pk
   -- And if there exists another v' that is also new and valid
   â†’ v' âŠ‚Msg m'  â†’ send m' âˆˆ outs
   â†’ (sig' : WithVerSig pk v') â†’ Â¬ (âˆˆGenInfo (ver-signature sig'))
   â†’ Â¬ (MsgWithSigâˆˆ pk (ver-signature sig') (msgPool pre)) -- âˆ„[ v'' ] VoteForRoundâˆˆ ... ?
   â†’ PeerCanSignForPK (StepPeer-post {pre = pre} (step-honest sps)) v' pid pk
   -- If v and v' share the same epoch and round
   â†’ v ^âˆ™ vEpoch â‰¡ v' ^âˆ™ vEpoch
   â†’ v ^âˆ™ vRound < v' ^âˆ™ vRound
   â†’ Î±-ValidVote ğ“” v  mbr â‰¡ vabs
   â†’ Î±-ValidVote ğ“” v' mbr â‰¡ v'abs
   â†’ (c2 : Cand-3-chain-vote vabs)
   â†’ Î£ (VoteParentData v'abs)
           (Î» vp â†’ Cand-3-chain-head-round c2 â‰¤ Abs.round (vpParent vp))

  -- Next, we prove that given the necessary obligations,
 module PR-Proof
   (sps-corr : StepPeerState-AllValidParts)
   (Impl-IRO : VO.IncreasingRoundObligation)
   (Impl-PR1 : PR-ImplObligationâ‚)
   (Impl-PR2 : PR-ImplObligationâ‚‚)
   where
  -- Any reachable state satisfies the PR rule for any epoch in the system.
  module _ (st : SystemState)(r : ReachableSystemState st) where
   -- Bring in 'unwind', 'ext-unforgeability' and friends
   open Structural sps-corr
   -- Bring in intSystemState
   open        PerState st r
   open        PerEpoch ğ“”
   open        ConcreteCommonProperties st r


   Î±-ValidVote-trans : âˆ€ {pk mbr vabs pool} (v : Vote)
                     â†’ Î±-ValidVote ğ“” v mbr â‰¡ vabs
                     â†’ (vfr : VoteForRoundâˆˆ pk (v ^âˆ™ vRound) (v ^âˆ™ vEpoch)
                                            (v ^âˆ™ vProposedId) pool)
                     â†’ Î±-ValidVote ğ“” (msgVote vfr) mbr â‰¡ vabs
   Î±-ValidVote-trans vâ‚ refl vfr
     with msgRoundâ‰¡ vfr | msgEpochâ‰¡ vfr | msgBIdâ‰¡ vfr
   ...| refl | refl | refl = refl

   PreferredRoundProof :
      âˆ€ {pk roundâ‚ roundâ‚‚ epoch bIdâ‚ bIdâ‚‚ vâ‚abs vâ‚‚abs mbr} {st : SystemState}
      â†’ ReachableSystemState st
      â†’ Meta-Honest-PK pk
      â†’ (vâ‚ : VoteForRoundâˆˆ pk roundâ‚ epoch bIdâ‚ (msgPool st))
      â†’ (vâ‚‚ : VoteForRoundâˆˆ pk roundâ‚‚ epoch bIdâ‚‚ (msgPool st))
      â†’ roundâ‚ < roundâ‚‚
      â†’ Î±-ValidVote ğ“” (msgVote vâ‚) mbr â‰¡ vâ‚abs
      â†’ Î±-ValidVote ğ“” (msgVote vâ‚‚) mbr â‰¡ vâ‚‚abs
      â†’ (c3 : Cand-3-chain-vote vâ‚abs)
      â†’ Î£ (VoteParentData vâ‚‚abs)
            (Î» vp â†’ Cand-3-chain-head-round c3 â‰¤ Abs.round (vpParent vp))
   PreferredRoundProof step@(step-s r theStep) pkH vâ‚ vâ‚‚ râ‚<râ‚‚ refl refl c3
      with msgRoundâ‰¡ vâ‚ | msgEpochâ‰¡ vâ‚ | msgBIdâ‰¡ vâ‚
         | msgRoundâ‰¡ vâ‚‚ | msgEpochâ‰¡ vâ‚‚ | msgBIdâ‰¡ vâ‚‚
   ...| refl | refl | refl | refl | refl | refl
      with âˆˆGenInfo? (â‚‹vSignature (msgVote vâ‚)) | âˆˆGenInfo? (â‚‹vSignature (msgVote vâ‚‚))
   ...| yes initâ‚  | yes initâ‚‚  = let râ‚â‰¡0 = genVotesRoundâ‰¡0 (msgSigned vâ‚) initâ‚
                                      râ‚‚â‰¡0 = genVotesRoundâ‰¡0 (msgSigned vâ‚‚) initâ‚‚
                                  in âŠ¥-elim (<â‡’â‰¢ râ‚<râ‚‚ (trans râ‚â‰¡0 (sym râ‚‚â‰¡0)))
   ...| yes initâ‚  | no  Â¬initâ‚‚ = let 0â‰¡rv = sym (genVotesRoundâ‰¡0 (msgSigned vâ‚) initâ‚)
                                      0<rv = v-cand-3-chainâ‡’0<roundv c3
                                  in âŠ¥-elim (<â‡’â‰¢ 0<rv 0â‰¡rv)
   ...| no  Â¬initâ‚ | yes initâ‚‚  = let 0â‰¡râ‚‚ = sym (genVotesRoundâ‰¡0 (msgSigned vâ‚‚) initâ‚‚)
                                      râ‚   = msgVote vâ‚ ^âˆ™ vRound
                                  in âŠ¥-elim (<â‡’â‰± râ‚<râ‚‚ (subst (râ‚ â‰¥_) 0â‰¡râ‚‚ zâ‰¤n))
   ...| no  Â¬initâ‚ | no Â¬initâ‚‚
      with theStep
   ...| step-peer cheat@(step-cheat c)
        = let mâ‚sb4 = Â¬cheatForgeNewSig r cheat unit pkH (msgSigned vâ‚) (msgâŠ† vâ‚) (msgâˆˆpool vâ‚) Â¬initâ‚
              mâ‚‚sb4 = Â¬cheatForgeNewSig r cheat unit pkH (msgSigned vâ‚‚) (msgâŠ† vâ‚‚) (msgâˆˆpool vâ‚‚) Â¬initâ‚‚
              vâ‚sb4 = msgSentB4â‡’VoteRoundâˆˆ (msgSigned vâ‚) mâ‚sb4
              vâ‚‚sb4 = msgSentB4â‡’VoteRoundâˆˆ (msgSigned vâ‚‚) mâ‚‚sb4
              vâ‚abs = Î±-ValidVote-trans (msgVote vâ‚) refl vâ‚sb4
              vâ‚‚abs = Î±-ValidVote-trans (msgVote vâ‚‚) refl vâ‚‚sb4
          in PreferredRoundProof r pkH vâ‚sb4 vâ‚‚sb4 râ‚<râ‚‚ vâ‚abs vâ‚‚abs c3
   ...| step-peer (step-honest stP)
      with âŠ-mapâ‚‚ (msgSentB4â‡’VoteRoundâˆˆ (msgSigned vâ‚))
                  (newMsgâŠmsgSentB4 r stP pkH (msgSigned vâ‚) Â¬initâ‚  (msgâŠ† vâ‚) (msgâˆˆpool vâ‚))
         | âŠ-mapâ‚‚ (msgSentB4â‡’VoteRoundâˆˆ (msgSigned vâ‚‚))
                  (newMsgâŠmsgSentB4 r stP pkH (msgSigned vâ‚‚) Â¬initâ‚‚ (msgâŠ† vâ‚‚) (msgâˆˆpool vâ‚‚))
   ...| injâ‚‚ vâ‚sb4                | injâ‚‚ vâ‚‚sb4
        = let vâ‚abs = Î±-ValidVote-trans (msgVote vâ‚) refl vâ‚sb4
              vâ‚‚abs = Î±-ValidVote-trans (msgVote vâ‚‚) refl vâ‚‚sb4
          in PreferredRoundProof r pkH vâ‚sb4 vâ‚‚sb4 râ‚<râ‚‚ vâ‚abs vâ‚‚abs c3
   ...| injâ‚ (mâ‚âˆˆouts , vâ‚pk , newVâ‚) | injâ‚ (mâ‚‚âˆˆouts , vâ‚‚pk , newVâ‚‚)
        = Impl-PR2 r stP pkH (msgâŠ† vâ‚) mâ‚âˆˆouts (msgSigned vâ‚) Â¬initâ‚ newVâ‚ vâ‚pk (msgâŠ† vâ‚‚)
                   mâ‚‚âˆˆouts (msgSigned vâ‚‚) Â¬initâ‚‚ newVâ‚‚ vâ‚‚pk refl râ‚<râ‚‚ refl refl c3
   ...| injâ‚ (mâ‚âˆˆouts , vâ‚pk , _) | injâ‚‚ vâ‚‚sb4
        = let roundâ‰¡ = trans (msgRoundâ‰¡ vâ‚‚sb4) (msgRoundâ‰¡ vâ‚‚)
              Â¬genVâ‚‚ = Â¬Genâˆ§Roundâ‰¡â‡’Â¬Gen step pkH vâ‚‚ Â¬initâ‚‚ (msgSigned vâ‚‚sb4) roundâ‰¡
              epochâ‰¡ = sym (msgEpochâ‰¡ vâ‚‚sb4)
          in either (Î» râ‚‚<râ‚ â†’ âŠ¥-elim (<â‡’â‰¯ râ‚<râ‚‚ (<-transÊ³ (â‰¡â‡’â‰¤ (sym roundâ‰¡)) râ‚‚<râ‚)))
                    (Î» vâ‚sb4 â†’ let vâ‚abs = Î±-ValidVote-trans (msgVote vâ‚) refl vâ‚sb4
                                   vâ‚‚abs = Î±-ValidVote-trans (msgVote vâ‚‚) refl vâ‚‚sb4
                               in PreferredRoundProof r pkH vâ‚sb4 vâ‚‚sb4 râ‚<râ‚‚ vâ‚abs vâ‚‚abs c3)
                    (Impl-IRO r stP pkH (msgâŠ† vâ‚) mâ‚âˆˆouts (msgSigned vâ‚) Â¬initâ‚ vâ‚pk
                              (msgâŠ† vâ‚‚sb4) (msgâˆˆpool vâ‚‚sb4) (msgSigned vâ‚‚sb4) Â¬genVâ‚‚ epochâ‰¡)
   ...| injâ‚‚ vâ‚sb4                | injâ‚ (mâ‚‚âˆˆouts , vâ‚‚pk , _)
        = let rvâ‚<râ‚‚ = <-transÊ³ (â‰¡â‡’â‰¤ (msgRoundâ‰¡ vâ‚sb4)) râ‚<râ‚‚
              roundâ‰¡ = trans (msgRoundâ‰¡ vâ‚sb4) (msgRoundâ‰¡ vâ‚)
              Â¬genVâ‚ = Â¬Genâˆ§Roundâ‰¡â‡’Â¬Gen step pkH vâ‚ Â¬initâ‚ (msgSigned vâ‚sb4) roundâ‰¡
              vâ‚abs = Î±-ValidVote-trans (msgVote vâ‚) refl vâ‚sb4
          in either id
                    (Î» vâ‚‚sb4 â†’ let vâ‚abs = Î±-ValidVote-trans (msgVote vâ‚) refl vâ‚sb4
                                   vâ‚‚abs = Î±-ValidVote-trans (msgVote vâ‚‚) refl vâ‚‚sb4
                               in PreferredRoundProof r pkH vâ‚sb4 vâ‚‚sb4 râ‚<râ‚‚ vâ‚abs vâ‚‚abs c3)
                    (Impl-PR1 r stP pkH (msgâŠ† vâ‚‚) mâ‚‚âˆˆouts (msgSigned vâ‚‚) Â¬initâ‚‚ vâ‚‚pk
                              (msgâŠ† vâ‚sb4) (msgâˆˆpool vâ‚sb4) (msgSigned vâ‚sb4) Â¬genVâ‚
                              (msgEpochâ‰¡ vâ‚sb4) rvâ‚<râ‚‚ vâ‚abs refl c3)


   prr : Type intSystemState
   prr honÎ± refl sv refl sv' c2 round<
     with vmsgâ‰ˆv (vmFor sv) | vmsgâ‰ˆv (vmFor sv')
   ...| refl | refl
       = let vâ‚ = mkVoteForRoundâˆˆ (nm (vmFor sv)) (cv ((vmFor sv))) (cvâˆˆnm (vmFor sv))
                                  (vmSender sv) (nmSentByAuth sv) (vmsgSigned (vmFor sv))
                                  (vmsgEpoch (vmFor sv)) refl refl
             vâ‚‚ = mkVoteForRoundâˆˆ (nm (vmFor sv')) (cv (vmFor sv')) (cvâˆˆnm (vmFor sv'))
                                  (vmSender sv') (nmSentByAuth sv') (vmsgSigned (vmFor sv'))
                                  (vmsgEpoch (vmFor sv')) refl refl
         in PreferredRoundProof r honÎ± vâ‚ vâ‚‚ round< refl refl c2
