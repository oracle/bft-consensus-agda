open import LibraBFT.Prelude
open import LibraBFT.Hash
open import LibraBFT.Lemmas
open import LibraBFT.Abstract.Types

module LibraBFT.Abstract.RecordChain 
  (ec  : EpochConfig)
  (UID : Set)
  (_â‰ŸUID_ : (uâ‚€ uâ‚ : UID) â†’ Dec (uâ‚€ â‰¡ uâ‚))
    where

 open import LibraBFT.Abstract.Records          ec UID _â‰ŸUID_
 open import LibraBFT.Abstract.Records.Extends  ec UID _â‰ŸUID_
 open import LibraBFT.Abstract.RecordStoreState ec UID _â‰ŸUID_

 module WithRSS
   {a}{RSS : Set a}â¦ƒ isRSS : isRecordStoreState RSS â¦„
   -- The current record pool; abstracted by saying
   -- whether a record is in the pool or not.
   (curr : RSS)
     where

  IsInPool : Record â†’ Set
  IsInPool r = isInPool isRSS r curr

  IsInPool-irrelevant : âˆ€{r}(pâ‚€ pâ‚ : IsInPool r) â†’ pâ‚€ â‰¡ pâ‚
  IsInPool-irrelevant = isInPool-irrelevant isRSS

  -- A record chain is a slice of the reflexive transitive closure with
  -- valid records only. Validity, in turn, is defined by recursion on the
  -- chain.

  -- One way of looking at a 'RecordChain r' is to think of it as 
  -- one path from the epoch's initial record to r.
  data RecordChain : Record â†’ Set where
    empty : RecordChain I
    step  : âˆ€ {r r'}
          â†’ (rc : RecordChain r) 
          â†’ r â† r'
          â†’ {prf : IsInPool r'} -- TODO: Make these into instance arguments too!
          â†’ RecordChain r'

  prevBlock : âˆ€{q} â†’ RecordChain (Q q) â†’ Block
  prevBlock (step {r = B b} _ (Bâ†Q _ _)) = b

  -- Defition of 'previous_round' as in Paper Section 5.5
  currRound : âˆ€{r} â†’ RecordChain r â†’ Round
  currRound empty = 0
  currRound (step {r = r} _ _) = round r

  -- TODO: prev round should be defined for blocks only...
  prevRound : âˆ€{r} â†’ RecordChain r â†’ Round
  prevRound empty = 0
  prevRound (step rc (Iâ†B x vr)) = 0
  prevRound (step rc (Qâ†B x vr)) = currRound rc
  prevRound (step rc (Bâ†Q x vr)) = prevRound rc

  ----------------------
  -- RecordChain Irrelevance
  --

  -- Distributing a record relation pointwise
  -- over record chains. Let rcâ‚€ and rcâ‚ be as illustrated
  -- below; a value of type â‰ˆRC-pw, named prf is shown
  -- in between them.
  -- 
  --  rcâ‚€    : Bâ‚€ â† Câ‚€  â† Bâ‚ â† Câ‚ â† â‹¯ â† Bâ‚–  â† Câ‚–
  --
  --  prf      â‰ˆ    â‰ˆ     â‰ˆ    â‰ˆ        â‰ˆ     â‰ˆ
  --
  --  rcâ‚    : ğ“‘â‚€ â† ğ“’â‚€ â† ğ“‘â‚ â† ğ“’â‚ â† â‹¯ â† ğ“‘â‚– â† ğ“’â‚–
  --
  --
  data â‰ˆRC-pw {â„“}(_â‰ˆ_ : Rel Record â„“) 
      : âˆ€{râ‚€ râ‚} â†’ RecordChain râ‚€ â†’ RecordChain râ‚ â†’ Set â„“ where
    eq-empty : I â‰ˆ I â†’ â‰ˆRC-pw _â‰ˆ_ empty empty
    eq-step  : âˆ€{râ‚€ râ‚ sâ‚€ sâ‚}
             â†’ (rcâ‚€ : RecordChain sâ‚€)
             â†’ (rcâ‚ : RecordChain sâ‚)
             â†’ râ‚€ â‰ˆ râ‚
             â†’ (extâ‚€ : sâ‚€ â† râ‚€)
             â†’ (extâ‚ : sâ‚ â† râ‚)
             â†’ {pâ‚€ : IsInPool râ‚€}
             â†’ {pâ‚ : IsInPool râ‚}
             â†’ â‰ˆRC-pw _â‰ˆ_ rcâ‚€ rcâ‚
             â†’ â‰ˆRC-pw _â‰ˆ_ (step rcâ‚€ extâ‚€ {pâ‚€}) (step rcâ‚ extâ‚ {pâ‚})

  -- RecordChain equivalence is then defined in terms of
  -- record equivalence (i.e., we don't care about the set of
  -- votes for the QCs in the chain); borrowing the illustration
  -- above, we now have:
  --
  --  rcâ‚€    : Bâ‚€ â† Câ‚€  â† Bâ‚ â† Câ‚ â† â‹¯ â† Bâ‚–  â† Câ‚–
  --
  --  prf      â‰¡    â‰ˆQC   â‰¡    â‰ˆQC      â‰¡     â‰ˆQC
  --
  --  rcâ‚    : ğ“‘â‚€ â† ğ“’â‚€ â† ğ“‘â‚ â† ğ“’â‚ â† â‹¯ â† ğ“‘â‚– â† ğ“’â‚–
  --
  -- It is easy to see that if rcâ‚€ â‰ˆRC rcâ‚, then they contain
  -- the same blocks (propositionally!) but potentially 
  -- different /sets of votes/ certifying said blocks.
  _â‰ˆRC_ : âˆ€{râ‚€ râ‚} â†’ RecordChain râ‚€ â†’ RecordChain râ‚ â†’ Set
  _â‰ˆRC_ = â‰ˆRC-pw _â‰ˆRec_

  -- Heterogeneous irrelevance of _â‰ˆRC_ happens only modulo
  -- propositional non-injectivity of block ids; which is
  -- awesome!
  â‰ˆRC-refl : âˆ€{râ‚€ râ‚}(rcâ‚€ : RecordChain râ‚€)(rcâ‚ : RecordChain râ‚)
           â†’ râ‚€ â‰ˆRec râ‚
           â†’ NonInjective _â‰¡_ bId âŠ (rcâ‚€ â‰ˆRC rcâ‚)
  â‰ˆRC-refl empty empty hyp 
     = injâ‚‚ (eq-empty hyp)
  â‰ˆRC-refl (step r0 x) (step r1 xâ‚) hyp 
     = (â†-â‰ˆRec x xâ‚ hyp âŠâŸ«= â‰ˆRC-refl r0 r1)
        âŠâŸ«= (injâ‚‚ âˆ˜ eq-step r0 r1 hyp x xâ‚)
  â‰ˆRC-refl empty (step r1 (Iâ†B x xâ‚)) () 
  â‰ˆRC-refl empty (step r1 (Qâ†B x xâ‚)) () 
  â‰ˆRC-refl empty (step r1 (Bâ†Q x xâ‚)) () 
  â‰ˆRC-refl (step r0 (Iâ†B x xâ‚)) empty () 
  â‰ˆRC-refl (step r0 (Qâ†B x xâ‚)) empty () 
  â‰ˆRC-refl (step r0 (Bâ†Q x xâ‚)) empty () 


  -- Homogeneous irrelevance is easy to conjure:
  RecordChain-irrelevant : âˆ€{r}(rcâ‚€ rcâ‚ : RecordChain r) 
                         â†’ NonInjective _â‰¡_ bId âŠ rcâ‚€ â‰ˆRC rcâ‚
  RecordChain-irrelevant rc0 rc1 = â‰ˆRC-refl rc0 rc1 â‰ˆRec-refl

  -- A k-chain (paper Section 5.2) is a sequence of
  -- blocks and quorum certificates for said blocks:
  --
  --  Bâ‚€ â† Câ‚€ â† Bâ‚ â† Câ‚ â† â‹¯ â† Bâ‚– â† Câ‚–
  --
  -- Our datatype ğ•‚-chain captures exactly that structure.
  --
  data ğ•‚-chain (R : Record â†’ Record â†’ Set) 
      : (k : â„•){r : Record} â†’ RecordChain r â†’ Set where
    0-chain : âˆ€{r}{rc : RecordChain r} â†’ ğ•‚-chain R 0 rc
    s-chain : âˆ€{k r}{rc : RecordChain r}{b : Block}{q : QC}
            â†’ (râ†b : r   â† B b)
            â†’ {prfB : IsInPool (B b)}
            â†’ (prf : R r (B b))
            â†’ (bâ†q : B b â† Q q)
            â†’ {prfQ : IsInPool (Q q)}
            â†’ ğ•‚-chain R k rc
            â†’ ğ•‚-chain R (suc k) (step (step rc râ†b {prfB}) bâ†q {prfQ})

  -- Returns the round of the block heading the k-chain.
  kchainHeadRound : âˆ€{k r P}{rc : RecordChain r} â†’ ğ•‚-chain P k rc â†’ Round
  kchainHeadRound (0-chain {r = r})      = round r
  kchainHeadRound (s-chain râ†b _ bâ†q kk) = kchainHeadRound kk

  kchainBlock : âˆ€{k r P}{rc : RecordChain r} â†’ Fin k â†’ ğ•‚-chain P k rc â†’ Block
  kchainBlock zero    (s-chain {b = b} _ _ _ _) = b
  kchainBlock (suc x) (s-chain râ†b _ bâ†q kk)    = kchainBlock x kk

  _bâŸ¦_âŸ§ : âˆ€{k r P}{rc : RecordChain r} â†’ ğ•‚-chain P k rc â†’ Fin k â†’ Block
  chain bâŸ¦ ix âŸ§ = kchainBlock ix chain

  kchainQC : âˆ€{k r P}{rc : RecordChain r} â†’ Fin k â†’ ğ•‚-chain P k rc â†’ QC
  kchainQC zero    (s-chain {q = q} _ _ _ _) = q
  kchainQC (suc x) (s-chain râ†b _ bâ†q kk)    = kchainQC x kk

  kchainForget
    : âˆ€{P k r}{rc : RecordChain r}(c : ğ•‚-chain P k rc) â†’ RecordChain r
  kchainForget {rc = rc} _ = rc

  kchain-to-RecordChain-at-bâŸ¦âŸ§
    : âˆ€{P k r}{rc : RecordChain r}(c : ğ•‚-chain P k rc)(ix : Fin k)
    â†’ RecordChain (B (c bâŸ¦ ix âŸ§))
  kchain-to-RecordChain-at-bâŸ¦âŸ§ 0-chain ()
  kchain-to-RecordChain-at-bâŸ¦âŸ§ (s-chain {rc = rc} râ†b {pb} x bâ†q {pq} c) zero
    = (step rc râ†b {pb})
  kchain-to-RecordChain-at-bâŸ¦âŸ§ (s-chain râ†b x bâ†q c) (suc zz)
    = kchain-to-RecordChain-at-bâŸ¦âŸ§ c zz

  kchainBlockRoundZero-lemma
    : âˆ€{k q P}{rc : RecordChain (Q q)}(c : ğ•‚-chain P (suc k) rc)
    â†’ getRound (kchainBlock zero c) â‰¡ getRound q
  kchainBlockRoundZero-lemma (s-chain râ†b prf (Bâ†Q r h) c) = sym r

  kchainBlockRoundâ‰¤ : âˆ€{k r P}{rc : RecordChain r}(x y : Fin k)(kc : ğ•‚-chain P k rc)
                    â†’ x â‰¤Fin y â†’ getRound (kchainBlock y kc) â‰¤ getRound (kchainBlock x kc)
  kchainBlockRoundâ‰¤ zero zero (s-chain râ†b prf bâ†q kc) hyp = â‰¤-refl
  kchainBlockRoundâ‰¤ zero (suc y) (s-chain (Qâ†B r râ†b) prf bâ†q (s-chain râ†bâ‚ prfâ‚ (Bâ†Q refl bâ†qâ‚) kc)) hyp 
    = â‰¤-trans (kchainBlockRoundâ‰¤ zero y (s-chain râ†bâ‚ prfâ‚ (Bâ†Q refl bâ†qâ‚) kc) zâ‰¤n) (<â‡’â‰¤ r)
  kchainBlockRoundâ‰¤ (suc x) (suc y) (s-chain râ†b prf bâ†q kc) (sâ‰¤s hyp) 
    = kchainBlockRoundâ‰¤ x y kc hyp

  Contig : Record â†’ Record â†’ Set
  Contig r r' = round r' â‰¡ suc (round r)

  kchain-round-â‰¤-lemma'
    : âˆ€{k q}{rc : RecordChain (Q q)}(c3 : ğ•‚-chain Contig k rc)(ix : Fin k)
    â†’ getRound (c3 bâŸ¦ ix âŸ§) â‰¤ getRound q
  kchain-round-â‰¤-lemma' (s-chain râ†b x (Bâ†Q refl bâ†q) c3) zero = â‰¤-refl
  kchain-round-â‰¤-lemma' (s-chain (Iâ†B prf imp) refl (Bâ†Q refl _) 0-chain) (suc ()) 
  kchain-round-â‰¤-lemma' (s-chain (Qâ†B prf imp) x (Bâ†Q refl _) c2) (suc ix) 
    = â‰¤-trans (kchain-round-â‰¤-lemma' c2 ix) (â‰¤-unstep prf)

  Simple : Record â†’ Record â†’ Set
  Simple _ _ = Unit

  ğ•‚-chain-contig : (k : â„•){r : Record} â†’ RecordChain r â†’ Set
  ğ•‚-chain-contig = ğ•‚-chain Contig

  -- States that a given record belongs in a record chain.
  data _âˆˆRC_ (râ‚€ : Record) : âˆ€{râ‚} â†’ RecordChain râ‚ â†’ Set where
    here   : âˆ€{rc : RecordChain râ‚€} â†’ râ‚€ âˆˆRC rc
    there  : âˆ€{râ‚ râ‚‚}{rc : RecordChain râ‚}(p : râ‚ â† râ‚‚)
           â†’ râ‚€ âˆˆRC rc
           â†’ {prf : IsInPool râ‚‚}
           â†’ râ‚€ âˆˆRC (step rc p {prf})
    -- This is a very important rule! It is the equivalent of a
    -- /congruence/ on record chains and enables us to prove
    -- the ğ•‚-chain-âˆˆRC property, which is crucial, since we
    -- lost the ability to rewrite record chains
    transp : âˆ€{r}{rcâ‚€ rcâ‚ : RecordChain r}
           â†’ râ‚€ âˆˆRC rcâ‚€
           â†’ rcâ‚€ â‰ˆRC rcâ‚
           â†’ râ‚€ âˆˆRC rcâ‚

  kchainBlock-correct
    : âˆ€{P k q b}{rc : RecordChain (B b)}{bâ†q : B b â† Q q}{ipq : IsInPool (Q q)}
    â†’ (kc : ğ•‚-chain P k (step rc bâ†q {ipq}))
    â†’ (x : Fin k) â†’ (B (kc bâŸ¦ x âŸ§)) âˆˆRC rc
  kchainBlock-correct (s-chain râ†b prf bâ†q kc) zero = here 
  kchainBlock-correct (s-chain râ†b prf bâ†q (s-chain râ†bâ‚ prfâ‚ bâ†qâ‚ kc)) (suc x) 
    = there râ†b (there bâ†qâ‚ (kchainBlock-correct (s-chain râ†bâ‚ prfâ‚ bâ†qâ‚ kc) x))

  -- This is an extended form of RecordChain-irrelevance.
  -- Let rc be:
  --
  --  Bâ‚€ â† Câ‚€ â† Bâ‚ â† Câ‚ â† â‹¯ â† Bâ‚™ â† Câ‚™
  -- 
  -- The (c : ğ•‚-chain P k rc) is a predicate on the shape
  -- of rc, estabilishing it must be of the following shape:
  -- (where consecutive blocks satisfy P!)
  --
  --  Bâ‚€ â† Câ‚€ â† Bâ‚ â† Câ‚ â† â‹¯ â† Bâ‚™â‚‹â‚– â† Câ‚™â‚‹â‚– â‹¯ â† Bâ‚™â‚‹â‚ â† Câ‚™â‚‹â‚ â† Bâ‚™ â† Câ‚™
  --                           /\             /\            /
  --                     â‹¯ P â‚‹âŒŸ  âŒâ‚‹â‚‹â‚‹â‚‹ P â‚‹â‚‹â‚‹â‚‹âŒŸ  âŒâ‚‹â‚‹â‚‹â‚‹ P â‚‹â‚‹â‚‹âŒŸ
  --
  -- This property states that for any other record chain
  -- that contains one block b of the kchain above, it also contains
  -- the prefix of the kchain leading to b.
  -- 
  ğ•‚-chain-âˆˆRC : âˆ€{r k P}{rc : RecordChain r}
              â†’ (c : ğ•‚-chain P k rc)
              â†’ (x y : Fin k)
              â†’ x â‰¤Fin y
              â†’ {b : Block}(prf : kchainBlock x c â‰¡ b)
              â†’ (rcâ‚ : RecordChain (B b))
              â†’ NonInjective _â‰¡_ bId âŠ (B (kchainBlock y c) âˆˆRC rcâ‚)
  ğ•‚-chain-âˆˆRC (s-chain râ†b {inP} prf bâ†q c) zero y zâ‰¤n refl rc1 
    with RecordChain-irrelevant (step (kchainForget c) râ†b {inP}) rc1
  ...| injâ‚ hb   = injâ‚ hb
  ...| injâ‚‚ res  = injâ‚‚ (transp (kchainBlock-correct (s-chain râ†b {inP} prf bâ†q c) y) res)
  ğ•‚-chain-âˆˆRC (s-chain râ†b prf bâ†q c) (suc x) (suc y) (sâ‰¤s xâ‰¤y) hyp rc1 
    = ğ•‚-chain-âˆˆRC c x y xâ‰¤y hyp rc1

  ------------------------
  -- Lemma 1

{-
  -- LemmaS1-1 states that a record that has been flagged as 'valid' (paper section 4.2)
  -- depends upon the initial record.
  lemmaS1-1 : {r : Record}
            â†’ RecordChain r
            â†’ (I mkInitial) â†â‹† r
  lemmaS1-1 empty = ssRefl
  lemmaS1-1 {r} (step rc ext) = ssStep (lemmaS1-1 rc) ext
-}

  -----------------
  -- Commit Rule --

  -- A block (and everything preceeding it) is said to match the commit rule
  -- when the block is the head of a contiguious 3-chain. Here we define an auxiliary
  -- datatype to make definitions more bearable.
  data CommitRule : âˆ€{r} â†’ RecordChain r â†’ Block â†’ Set where
    commit-rule : âˆ€{r b}{rc : RecordChain r}(c3 : ğ•‚-chain Contig 3 rc) 
                â†’ b â‰¡ c3 bâŸ¦ suc (suc zero) âŸ§
                â†’ CommitRule rc b

  voteâ‰¡â‡’QPrevHashâ‰¡ : {q q' : QC} {v v' : Vote} 
                   â†’ v  âˆˆ qcVotes q
                   â†’ v' âˆˆ qcVotes q'
                   â†’ v â‰¡ v' 
                   â†’ qPrev q â‰¡ qPrev q'
  voteâ‰¡â‡’QPrevHashâ‰¡ {q} {q'} vâˆˆq v'âˆˆq' refl
      with witness vâˆˆq (qVotes-C3 q) | witness v'âˆˆq' (qVotes-C3 q')
  ... | refl | refl = refl

  voteâ‰¡â‡’QRoundâ‰¡ : {q q' : QC} {v v' : Vote} 
                â†’ v  âˆˆ qcVotes q
                â†’ v' âˆˆ qcVotes q'
                â†’ v â‰¡ v' 
                â†’ getRound q â‰¡ getRound q'
  voteâ‰¡â‡’QRoundâ‰¡ {q} {q'} vâˆˆq v'âˆˆq' refl
      with witness vâˆˆq (qVotes-C4 q) | witness v'âˆˆq' (qVotes-C4 q')
  ... | refl | refl = refl

  Â¬bRoundâ‰¡0 : âˆ€ {b} â†’ RecordChain (B b) â†’ Â¬ (getRound b â‰¡ 0)
  Â¬bRoundâ‰¡0 (step s (Iâ†B () h)) refl
  Â¬bRoundâ‰¡0 (step s (Qâ†B () h)) refl

