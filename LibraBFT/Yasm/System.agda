{- Byzantine Fault Tolerant Consensus Verification in Agda, version 0.9.

   Copyright (c) 2020 Oracle and/or its affiliates.
   Licensed under the Universal Permissive License v 1.0 as shown at https://opensource.oracle.com/licenses/upl
-}
{-# OPTIONS --allow-unsolved-metas #-}
open import LibraBFT.Prelude
open import LibraBFT.Lemmas
open import LibraBFT.Base.PKCS
open import LibraBFT.Base.Types
open import LibraBFT.Abstract.Types -- TODO-2: remove this, see comment below

open import LibraBFT.Yasm.AvailableEpochs using (AvailableEpochs) renaming (lookup'' to EC-lookup)
import LibraBFT.Yasm.AvailableEpochs as AE

open import LibraBFT.Yasm.Base

-- This module defines a model of a distributed system, parameterized by
-- SystemParameters, which establishes various application-dependent types,
-- handlers, etc.  The model supports a set of peers executing handlers in
-- response to messages received; these handlers can update the peer's
-- local state and/or send additional messages.  The model also enables
-- "cheat" steps, which can send arbitrary messages, except that they are
-- constrained to prevent a cheat step from introducing a new signature for
-- an "honest" public key.  The module also contains some structures for
-- proving properties of executions of the modeled system.

module LibraBFT.Yasm.System (parms : SystemParameters) where
 open SystemParameters parms

 -- TODO-2: The System model currently depends on a specific EpochConfig
 -- type, which is imported from LibraBFT-specific types.  However, the
 -- system model should be entirely application-independent.  Therefore, we
 -- should factor EpochConfig out of Yasm, and have the SystemParameters
 -- include an EpochConfig type and a way to query whether a given peer is
 -- a member of the represented epoch, and if so, with what associated PK.
 open EpochConfig

 PeerId : Set -- TODO-2: When we factor EpochConfig out of here (see
              -- comment above), PeerId will be a parameter to
              -- SystemParameters; for now, it's NodeId to make it
              -- compatible with everything else.
 PeerId = NodeId

 SenderMsgPair : Set
 SenderMsgPair = PeerId Ã— Msg

 SentMessages : Set
 SentMessages = List SenderMsgPair

 -- The model supports sending messages that contain some fields that are
 -- /not/ covered by the message's signature.  Therefore, given a message
 -- with a verifiable signature, it is possible for a propositionally
 -- different message that verifies against the same signature to have been
 -- sent before, which is captured by the following definition.
 record MsgWithSigâˆˆ (pk : PK)(sig : Signature)(pool : SentMessages) : Set where
   constructor mkMsgWithSigâˆˆ
   field
     msgWhole   : Msg
     msgPart    : Part
     msgâŠ†       : msgPart âŠ‚Msg msgWhole
     msgSender  : PeerId
     msgâˆˆpool   : (msgSender , msgWhole) âˆˆ pool
     msgSigned  : WithVerSig pk msgPart
     msgSameSig : ver-signature msgSigned â‰¡ sig
 open MsgWithSigâˆˆ public

 postulate  -- TODO-1: prove it
   MsgWithSigâˆˆ? : âˆ€ {pk} {sig} {pool} â†’ Dec (MsgWithSigâˆˆ pk sig pool)

 MsgWithSigâˆˆ-++Ê³ : âˆ€{pk sig pool ms} â†’ MsgWithSigâˆˆ pk sig pool â†’ MsgWithSigâˆˆ pk sig (ms ++ pool)
 MsgWithSigâˆˆ-++Ê³ {ms = pre} msig = record
    { msgWhole   = msgWhole msig
    ; msgPart    = msgPart  msig
    ; msgâŠ†       = msgâŠ†     msig
    ; msgâˆˆpool   = Any-++Ê³ pre (msgâˆˆpool msig)
    ; msgSigned  = msgSigned msig
    ; msgSameSig = msgSameSig msig
    }

 MsgWithSigâˆˆ-++Ë¡ : âˆ€{pk sig pool ms} â†’ MsgWithSigâˆˆ pk sig ms â†’ MsgWithSigâˆˆ pk sig (ms ++ pool)
 MsgWithSigâˆˆ-++Ë¡ {ms = pre} msig = record
    { msgWhole   = msgWhole msig
    ; msgPart    = msgPart  msig
    ; msgâŠ†       = msgâŠ†     msig
    ; msgâˆˆpool   = Any-++Ë¡ (msgâˆˆpool msig)
    ; msgSigned  = msgSigned msig
    ; msgSameSig = msgSameSig msig
    }

 MsgWithSigâˆˆ-transp : âˆ€{pk sig pool pool'}
                    â†’ (mws : MsgWithSigâˆˆ pk sig pool)
                    â†’ (msgSender mws , msgWhole mws) âˆˆ pool'
                    â†’ MsgWithSigâˆˆ pk sig pool'
 MsgWithSigâˆˆ-transp msig âˆˆpool' = record
    { msgWhole   = msgWhole msig
    ; msgPart    = msgPart  msig
    ; msgâŠ†       = msgâŠ†     msig
    ; msgâˆˆpool   = âˆˆpool'
    ; msgSigned  = msgSigned msig
    ; msgSameSig = msgSameSig msig
    }


 -- * Forbidding the Forging of Signatures
 --
 -- Whenever our reasoning must involve digital signatures, it is standard
 -- to talk about EUF-CMA resistant signature schemes. Informally, this captures
 -- signatures schemes that cannot be compromised by certain adversaries.
 -- Formally, it means that for any probabilistic-polynomial-time adversary ğ“,
 -- and some security parameter k:
 --
 --      Pr[ EUF-CMA(k) ] â‰¤ Îµ(k) for Îµ a negigible function.
 --
 -- EUF-CMA is defined as:
 --
 -- EUF-CMA(k):                           |  O(m):
 --   L        â† âˆ…                        |    Ïƒ â† Sign(sk , m)
 --   (pk, sk) â† Gen(k)                   |    L â† L âˆª { m }
 --   (m , Ïƒ)  â† ğ“á´¼(pk)                   |    return Ïƒ
 --   return (Verify(pk, m, Ïƒ) âˆ§ m âˆ‰ L)   |
 --
 -- This says that ğ“ cannot create a message that has /not yet been signed/ and
 -- forge a signature for it. The list 'L' keeps track of the messages that ğ“
 -- asked to be signed by the oracle.
 --
 -- Because the probability of the adversary to win the EUF-CMA(k) game
 -- approaches 0 as k increases; it is reasonable to assume that winning
 -- the game is /impossible/ for realistic security parameters.
 --
 -- EUF-CMA security is incorporated into our model by constraining messages
 -- sent by a cheat step to ensure that for every verifiably signed part of
 -- such a message, if there is a signature on the part, then it is either for
 -- a dishonest public key (in which cases it's secret key may have been leaked),
 -- or a message has been sent with the same signature before (in which case the
 -- signature is simply being "replayed" from a previous message).
 --
 -- Dishonest (or "cheat") messages are those that are not the result of
 -- a /handle/ or /init/ call, but rather are the result of a /cheat/ step.
 --
 -- A part of a cheat message can contain a verifiable signature only if it
 -- is for a dishonest public key, or a message with the same signature has
 -- been sent before (a cheater can "reuse" an honest signature sent
 -- before; it just can't produce a new one).  Note that this constraint
 -- precludes a peer sending a message that contains a new verifiable
 -- signature for an honest PK, even if the PK is the peer's own PK for
 -- some epoch (implying that the peer possesses the associated secret
 -- key).  In other words, a peer that is honest for a given epoch (by
 -- virtue of being a member of that epoch and being assigned an honest PK
 -- for the epoch), cannot send a message for that epoch using a cheat
 -- step.
 CheatPartConstraint : SentMessages â†’ Part â†’ Set
 CheatPartConstraint pool m = âˆ€{pk} â†’ (ver : WithVerSig pk m)
                                    â†’ Meta-Dishonest-PK pk
                                    âŠ MsgWithSigâˆˆ pk (ver-signature ver) pool

 -- The only constraints on messages sent by cheat steps are that:
 -- * the sender is not an honest member in the epoch of any part of the message
 -- * the signature on any part of the message satisfies CheatCanSign, meaning
 --   that it is not a new signature for an honest public key
 CheatMsgConstraint : SentMessages â†’ Msg â†’ Set
 CheatMsgConstraint pool m = âˆ€{part} â†’ part âŠ‚Msg m â†’ CheatPartConstraint pool part

 -- * The System State
 --
 -- A system consists in a partial map from PeerId to PeerState, a pool
 -- of sent messages and a number of available epochs.
 record SystemState (e : â„•) : Set where
   field
     peerStates  : Map PeerId PeerState
     msgPool     : SentMessages          -- All messages ever sent
     availEpochs : AvailableEpochs e
 open SystemState public

 initialState : SystemState 0
 initialState = record
   { peerStates  = Map-empty
   ; msgPool     = []
   ; availEpochs = []
   }

 -- Convenience function for appending an epoch to the system state
 pushEpoch : âˆ€{e} â†’ EpochConfigFor e â†’ SystemState e â†’ SystemState (suc e)
 pushEpoch ğ“” st = record
   { peerStates  = peerStates st
   ; msgPool     = msgPool st
   ; availEpochs = AE.append ğ“” (availEpochs st)
   }

 -- * Small Step Semantics
 --
 -- The small step semantics are divided into three datatypes:
 --
 -- i)   StepPeerState executes a step through init or handle
 -- ii)  StepPeer executes a step through StepPeerState or cheat
 -- iii) Step transitions the system state by a StepPeer or by
 --      bringing a new epoch into existence

 -- The pre and post states of Honest peers are related iff
 data StepPeerState {e}(pid : PeerId)(ğ“”s : AvailableEpochs e)(pool : SentMessages)
          : Maybe PeerState â†’ PeerState â†’ List Msg â†’ Set where
   -- The peer receives an "initialization package"; for now, this consists
   -- of the actual EpochConfig for the epoch being initialized.  Later, we
   -- may move to a more general scheme, enabled by assuming a function
   -- 'render : InitPackage -> EpochConfig'.
   step-init : âˆ€{ms s' out}(ix : Fin e)
             â†’ (s' , out) â‰¡ init pid (AE.lookup' ğ“”s ix) ms
             â†’ StepPeerState pid ğ“”s pool ms s' out

   -- The peer processes a message in the pool
   step-msg  : âˆ€{m ms s s' out}
             â†’ m âˆˆ pool
             â†’ ms â‰¡ just s â†’ (s' , out) â‰¡ handle pid (projâ‚‚ m) s
             â†’ StepPeerState pid ğ“”s pool ms s' out

 -- The pre-state of the suplied PeerId is related to the post-state and list of output messages iff:
 data StepPeer {e}(pre : SystemState e) : PeerId â†’ Maybe PeerState â†’ List Msg â†’ Set where
   -- it can be obtained by a handle or init call.
   step-honest : âˆ€{pid st outs}
               â†’ StepPeerState pid (availEpochs pre) (msgPool pre) (Map-lookup pid (peerStates pre)) st outs
               â†’ StepPeer pre pid (just st) outs

   -- or the peer decides to cheat.  CheatMsgConstraint ensures it cannot
   -- forge signatures by honest peers.  Cheat steps do not modify peer
   -- state: these are maintained exclusively by the implementation
   -- handlers.
   step-cheat  : âˆ€{pid}
               â†’ (fm : SentMessages â†’ Maybe PeerState â†’ Msg)
               â†’ let m = fm (msgPool pre) (Map-lookup pid (peerStates pre))
                  in CheatMsgConstraint (msgPool pre) m
                   â†’ StepPeer pre pid (Map-lookup pid (peerStates pre)) (m âˆ· [])

 isCheat : âˆ€ {e pre pid ms outs} â†’ StepPeer {e} pre pid ms outs â†’ Set
 isCheat (step-honest _)  = âŠ¥
 isCheat (step-cheat _ _) = Unit

 -- Computes the post-sysstate for a given step-peer.
 StepPeer-post : âˆ€{e pid st' outs}{pre : SystemState e}
               â†’ StepPeer pre pid st' outs â†’ SystemState e
 StepPeer-post {e} {pid} {st'} {outs} {pre} _ = record pre
   { peerStates = Map-set pid st' (peerStates pre)
   ; msgPool    = List-map (pid ,_) outs ++ msgPool pre
   }

 data Step : âˆ€{e e'} â†’ SystemState e â†’ SystemState e' â†’ Set where
   step-epoch : âˆ€{e}{pre : SystemState e}
              â†’ (ğ“” : EpochConfigFor e)
              -- TODO-3: Eventually, we'll condition this step to only be
              -- valid when peers on the previous epoch have agreed that ğ“”
              -- is the new one.  â†’ âˆƒEnoughValidCommitMsgsFor pre ğ“”
              â†’ Step pre (pushEpoch ğ“” pre)

   step-peer : âˆ€{e pid st' outs}{pre : SystemState e}
             â†’ (pstep : StepPeer pre pid st' outs)
             â†’ Step pre (StepPeer-post pstep)

 postulate -- TODO-1: prove it
   msgs-stable : âˆ€ {e e'} {pre : SystemState e} {post : SystemState e'} {m}
               â†’ (theStep : Step pre post)
               â†’ m âˆˆ msgPool pre
               â†’ m âˆˆ msgPool post

 postulate -- not used yet, but some proofs could probably be cleaned up using this,
           -- e.g., prevVoteRndâ‰¤-pred-step in Impl.VotesOnce
   sendMessages-target : âˆ€ {m : SenderMsgPair} {sm : SentMessages} {ml : List SenderMsgPair}
                       â†’ Â¬ (m âˆˆ sm)
                       â†’ m âˆˆ (ml ++ sm)
                       â†’ m âˆˆ ml

 step-epoch-does-not-send : âˆ€ {e} (pre : SystemState e) (ğ“” : EpochConfigFor e)
                            â†’ msgPool (pushEpoch ğ“” pre) â‰¡ msgPool pre
 step-epoch-does-not-send _ _ = refl

 -- * Reflexive-Transitive Closure

 data Step* : âˆ€{e e'} â†’ SystemState e â†’ SystemState e' â†’ Set where
   step-0 : âˆ€{e}{pre : SystemState e}
          â†’ Step* pre pre

   step-s : âˆ€{e e' e''}{fst : SystemState e}{pre : SystemState e'}{post : SystemState e''}
          â†’ Step* fst pre
          â†’ Step pre post
          â†’ Step* fst post

 ReachableSystemState : âˆ€{e} â†’ SystemState e â†’ Set
 ReachableSystemState = Step* initialState

 Step*-mono : âˆ€{e e'}{st : SystemState e}{st' : SystemState e'}
            â†’ Step* st st' â†’ e â‰¤ e'
 Step*-mono step-0 = â‰¤-refl
 Step*-mono (step-s tr (step-peer _)) = Step*-mono tr
 Step*-mono (step-s tr (step-epoch _)) = â‰¤-step (Step*-mono tr)

 MsgWithSigâˆˆ-Step* : âˆ€{e e' sig pk}{st : SystemState e}{st' : SystemState e'}
                   â†’ Step* st st'
                   â†’ MsgWithSigâˆˆ pk sig (msgPool st)
                   â†’ MsgWithSigâˆˆ pk sig (msgPool st')
 MsgWithSigâˆˆ-Step* step-0        msig = msig
 MsgWithSigâˆˆ-Step* (step-s tr (step-epoch _)) msig
   = MsgWithSigâˆˆ-Step* tr msig
 MsgWithSigâˆˆ-Step* (step-s tr (step-peer ps)) msig
   = MsgWithSigâˆˆ-++Ê³ (MsgWithSigâˆˆ-Step* tr msig)

 MsgWithSigâˆˆ-Step*-part : âˆ€{e e' sig pk}{st : SystemState e}{st' : SystemState e'}
                        â†’ (tr   : Step* st st')
                        â†’ (msig : MsgWithSigâˆˆ pk sig (msgPool st))
                        â†’ msgPart msig â‰¡ msgPart (MsgWithSigâˆˆ-Step* tr msig)
 MsgWithSigâˆˆ-Step*-part step-0        msig = refl
 MsgWithSigâˆˆ-Step*-part (step-s tr (step-epoch _)) msig
   = MsgWithSigâˆˆ-Step*-part tr msig
 MsgWithSigâˆˆ-Step*-part (step-s tr (step-peer ps)) msig
   = MsgWithSigâˆˆ-Step*-part tr msig

 ------------------------------------------

 -- Type synonym to express a relation over system states;
 SystemStateRel : (âˆ€{e e'} â†’ SystemState e â†’ SystemState e' â†’ Set) â†’ Setâ‚
 SystemStateRel P = âˆ€{e e'}{st : SystemState e}{st' : SystemState e'} â†’ P st st' â†’ Set

 -- Just like Data.List.Any maps a predicate over elements to a predicate over lists,
 -- Any-step maps a relation over steps to a relation over steps in a trace.
 data Any-Step (P : SystemStateRel Step) : SystemStateRel Step* where
  step-here  : âˆ€{e e' e''}{fst : SystemState e}{pre : SystemState e'}{post : SystemState e''}
             â†’ (cont : Step* fst pre)
             â†’ {this : Step pre post}(prf  : P this)
             â†’ Any-Step P (step-s cont this)

  step-there : âˆ€{e e' e''}{fst : SystemState e}{pre : SystemState e'}{post : SystemState e''}
             â†’ {cont : Step* fst pre}
             â†’ {this : Step pre post}
             â†’ (prf  : Any-Step P cont)
             â†’ Any-Step P (step-s cont this)

 Any-Step-â‡’ : âˆ€ {P Q : SystemStateRel Step}
            â†’ (âˆ€ {e e'}{pre : SystemState e}{post : SystemState e'} â†’ (x : Step pre post) â†’ P {e} {e'} x â†’ Q {e} {e'} x)
            â†’ âˆ€ {e e' fst lst} {tr : Step* {e} {e'} fst lst}
            â†’ Any-Step P tr
            â†’ Any-Step Q tr
 Any-Step-â‡’ pâ‡’q (step-here cont {this} prf) = step-here cont (pâ‡’q this prf)
 Any-Step-â‡’ pâ‡’q (step-there anyStep) = step-there (Any-Step-â‡’ pâ‡’q anyStep)

 Any-Step-elim
   : âˆ€{eâ‚€ eâ‚}{stâ‚€ : SystemState eâ‚€}{stâ‚ : SystemState eâ‚}{P : SystemStateRel Step}{Q : Set}
   â†’ {r : Step* stâ‚€ stâ‚}
   â†’ (Pâ‡’Q : âˆ€{d d'}{s : SystemState d}{s' : SystemState d'}{st : Step s s'}
          â†’ P st â†’ Step* s' stâ‚ â†’ Q)
   â†’ Any-Step P r â†’ Q
 Any-Step-elim Pâ‡’Q (step-here cont prf)
   = Pâ‡’Q prf step-0
 Any-Step-elim Pâ‡’Q (step-there {this = this} f)
   = Any-Step-elim (Î» p s â†’ Pâ‡’Q p (step-s s this)) f

 ------------------------------------------

 module _ (P : âˆ€{e} â†’ SystemState e â†’ Set) where

   Step*-Step-fold : (âˆ€{e}{st : SystemState e}
                        â†’ ReachableSystemState st
                        â†’ (ğ“” : EpochConfigFor e)
                        â†’ P st
                        â†’ P (pushEpoch ğ“” st))
                   â†’ (âˆ€{e pid st' outs}{st : SystemState e}
                        â†’ ReachableSystemState st
                        â†’ (pstep : StepPeer st pid st' outs)
                        â†’ P st
                        â†’ P (StepPeer-post pstep))
                   â†’ P initialState
                   â†’ âˆ€{e}{st : SystemState e}
                   â†’ (tr : ReachableSystemState st) â†’ P st
   Step*-Step-fold fe fs pâ‚€ step-0 = pâ‚€
   Step*-Step-fold fe fs pâ‚€ (step-s tr (step-epoch ğ“”)) = fe tr ğ“” (Step*-Step-fold fe fs pâ‚€ tr)
   Step*-Step-fold fe fs pâ‚€ (step-s tr (step-peer p)) = fs tr p (Step*-Step-fold fe fs pâ‚€ tr)
